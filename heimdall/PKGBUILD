# Contributor: Todd Musall 
# Contributor: dront78 (Ivan)
# Maintainer: Victor Noel
pkgname=('heimdall')
pkgver=1.3
pkgrel=1
pkgdesc="Heimdall is a cross-platform open-source utility to flash firmware (aka ROMs) onto Samsung Galaxy S devices."
arch=('i686' 'x86_64')
url="http://www.glassechidna.com.au/products/heimdall/"
license=('MIT')
depends=('libusb' 'qt')
optdepends=('udev-android-rules: Udev rules to connect Android devices to you linux box')
makedepends=('gcc')
source=('heimdall-1.3.tar.gz::http://github.com/Benjamin-Dobell/Heimdall/tarball/a9ba51f99ec1181874e4d018ea3d4b19a3eff6f7'
        'heimdall.desktop')
md5sums=('900ad97c0a36e431dc29a3a0c6929280'
         '6c4de9e74c0d9e7ab4d50af21303b78a')

build() {
  cd ${srcdir}/Benjamin-Dobell-Heimdall-*

  # Build libpit which is needed for compiling heimdall
  cd libpit/
  ./configure --prefix=/usr
  make

  # Build heimdall command line tool
  cd ../heimdall/
  
  ./configure --prefix=/usr
  make

  # Build heimdall GUI front end
  cd ../heimdall-frontend/

  qmake OUTPUTDIR="${pkgdir}/usr/bin" heimdall-frontend.pro
  make
}

package() {
  cd ${srcdir}/Benjamin-Dobell-Heimdall-*

  # Install heimdall command line tool
  cd heimdall/

  # Prevent make install from trying to reload udev
  # We'll do this the Arch way at package install time
  mv Makefile Makefile.orig
  sed -e 's/sudo service udev restart/echo sudo service udev restart/' <Makefile.orig >Makefile

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/lib/"
  install -m644 -D LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Install heimdall GUI front end
  cd ../heimdall-frontend
  make DESTDIR="${pkgdir}/" install
  install -m644 -D "${srcdir}/heimdall.desktop" "${pkgdir}/usr/share/applications/heimdall.desktop"
}
