pkgname=icinga
pkgver=1.7.2
pkgrel=3
pkgdesc="An open source host, service and network monitoring program."
license=('GPL')
arch=('i686' 'x86_64')
url="http://www.icinga.org"
depends=('gd' 'freetype2' 'libtool' 'libdbi-drivers' 'openssl')
optdepends=('nagios-plugins: plugins needed for icinga checks')
source=("http://downloads.sourceforge.net/project/icinga/icinga/$pkgver/$pkgname-$pkgver.tar.gz" 
        "rc.icinga" "systemd.icinga" "icinga.install")
install='icinga.install'

_icinga_user="icinga"
_icinga_group="icinga"

getent group ${_icinga_group} >/dev/null || _icinga_group=667 >/dev/null
getent passwd ${_icinga_user} >/dev/null || _icinga_user=667 >/dev/null

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix="/usr/share/icinga" \
    --datarootdir="/usr/share/webapps/icinga" \
		--sbindir="/usr/share/webapps/icinga/cgi-bin" \
    --bindir="/usr/bin" \
    --localstatedir="/var/icinga" \
    --sysconfdir="/etc/icinga" \
    --with-log-dir="/var/log/icinga" \
    --with-icinga-user=${_icinga_user} \
    --with-icinga-group=${_icinga_user} \
    --with-web-user=http \
    --with-web-group=http \
    --with-httpd-conf="/etc/httpd/conf/extra" \
    --with-checkresultdir="/var/icinga/spool/checkresults" \
    --enable-idoutils \
    --enable-embedded-perl \
    --enable-ssl

  make all
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install \
    install-idoutils \
    install-config

  install -D -m755 daemon-init "$pkgdir/etc/icinga/daemon-init"
  install -D -m755 "../rc.icinga" "$pkgdir/etc/rc.d/icinga"
  install -D -m644 "../systemd.icinga" "$pkgdir/usr/lib/systemd/system/icinga.service"
  install -D -m644 sample-config/httpd.conf "$pkgdir/etc/icinga/apache.example.conf"

  install -dm755 "$pkgdir/usr/share/icinga/idoutils"
  cp -r module/idoutils/db "$pkgdir/usr/share/icinga/idoutils"

  mkdir -p "$pkgdir/var/icinga/rw"
  chown ${_icinga_user}:${_icinga_group} "$pkgdir/var/icinga/rw"

  find $pkgdir/etc/icinga -name '*cfg' -exec mv "{}" "{}.sample" \; > /dev/null

  # Do some perms fixing
  find $pkgdir/usr/share/icinga -type f -exec chmod 644 "{}" \; > /dev/null
  find $pkgdir/usr/share/icinga -type d -exec chmod 755 "{}" \; > /dev/null
  find $pkgdir/etc/icinga -type f -exec chmod 644 "{}" \; > /dev/null
  find $pkgdir/etc/icinga -type d -exec chmod 755 "{}" \; > /dev/null
  chmod -R 755 $pkgdir/usr/bin
  chmod 755 $pkgdir/usr/share/webapps/icinga/cgi-bin/*
  chmod 755 $pkgdir/usr/share/icinga/lib/idomod.so
  chmod 755 $pkgdir/etc/icinga/daemon-init
  chown -R root:root $pkgdir/etc
  chown -R root:root $pkgdir/usr/bin
  chown -R root:root $pkgdir/usr/share/icinga
}

md5sums=('069b9a865bab7fd48be1b6da2953f5c7'
         '3d08782ae1c41d1c9735510e686a0a22'
         '69bc47b5887e4522cce14b378e981d40'
         '73f4dfb35e1e809130cb123d10821e1f')
